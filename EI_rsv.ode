# Six-variable r-s-v firing rate model for the E-I network
# April 20, 2023
# The following parameters (Table 4) reproduce the time course in Fig 7A for PING
# Changing Ii to 0.5 generates the time course in Fig 7B for ING

# parameters
par gee=2,gei=6,gie=2,gii=2,vebar=1,vibar=-0.1,ie=-0.2,ii=0
par thfe=0.2,kfe=0.05,thqe=0.5,kqe=0.1
par thfi=0.4,kfi=0.01,thqi=0.5,kqi=0.1
par gammae=3,gammai=3,s0e=0.2,s0i=0.05
par taure=1,tause=3,tauve=3,tauri=2,tausi=10,tauvi=5

# functions
fe(v)=1/(1+exp(-(v-thfe)/kfe))
qe(r)=1/(1+exp(-(r-thqe)/kqe))
fi(v)=1/(1+exp(-(v-thfi)/kfi))
qi(r)=1/(1+exp(-(r-thqi)/kqi))

# initial conditions
init re=0.6,se=0.6,ve=0.2,ri=0.1,si=0.05,vi=0.1

# ODEs
re'=(-re+fe(ve))/taure
se'=(-se+gammae*qe(re)*(1-se)+s0e)/tause
ve'=(-ve+gee*se*(vebar-ve)+gei*si*(vibar-ve)+ie)/tauve
ri'=(-ri+fi(vi))/tauri
si'=(-si+gammai*qi(ri)*(1-si)+s0i)/tausi
vi'=(-vi+gie*se*(vebar-vi)+gii*si*(vibar-vi)+ii)/tauvi

# synaptic currents
aux Isynee=gee*se*(vebar-ve)
aux Isynei=gei*si*(vibar-ve)
aux Isynie=gie*se*(vebar-vi)
aux Isynii=gii*si*(vibar-vi)

# numerics
@ total=100, dt=0.05, method=Runge-Kutta
@ xp=t,yp=re,xlo=0,xhi=100,ylo=-.1,yhi=1.1
@ yp2=ri,nplot=2
done
